// See https://github.com/JetBrains/gradle-intellij-plugin/
// Using Gradle IntelliJ Plugin (2.x)
import org.jetbrains.intellij.platform.gradle.TestFrameworkType

plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.10.2'
    id 'org.jetbrains.changelog' version '2.4.0'
}

group = 'org.sanchouss.idea.plugins.instantpatch'
version = '0.1.0'

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

test {
    enabled = false // tests are not run to save build time
}

tasks.named("buildSearchableOptions") {
    enabled = false // to save build time
}

// reason was lost ...
// buildSearchableOptions {
//     jvmArgs '--add-exports=java.base/jdk.internal.vm=ALL-UNNAMED'
// }
// runIde {
//     jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
// }

dependencies {
    implementation('com.github.mwiede:jsch:2.27.3')
//    compileOnly('avax.xml.bind:jaxb-api:2.3.1')  // Use javax but DON'T bundle it (to avoid classloaders clash). Pkg is moved out of JDK since Java 9. Alt is shadowing with plugin 'com.github.johnrengelman.shadow'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.2'
//    implementation('com.jcraft:jsch:0.1.55')  // abandoned, do not support modern auth methods
//    implementation('org.slf4j:slf4j-api:1.7.25')      // todo: remove
//    implementation('javax.xml.bind:jaxb-api:2.3.0')   // don't; this would cause two classloaders' conflict when debugging plugin
//    implementation('com.intellij:openapi:7.0.3')  // don't; this would cause two classloaders' conflict when debugging plugin

    intellijPlatform {
        testFramework TestFrameworkType.Platform.INSTANCE
    }
    testImplementation 'junit:junit:4.13.2'

    intellijPlatform {
        intellijIdeaCommunity '2025.2'
        bundledPlugin 'com.intellij.java'
    }
}

patchPluginXml {
    sinceBuild.set("231") // IDE build number, JavaVersion.VERSION_17 limits to higher than 221
//    untilBuild.set("252.*") // omit for now
    changeNotes.set("""
        <ul>
            <li>Compact notifications about network connection events</li>
            <li>Migrate to `Gradle IntelliJ Plugin (2.x)`</li>
            <li>Fix plugin verification issues</li>
        </ul>
    """)
}
